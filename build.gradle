plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.github.spotbugs" version "4.7.1"
    id "com.github.node-gradle.node" version "1.5.3"
}

node {
    version = '10.14.1'
    npmVersion = '6.4.1'
    download = true
    workDir = file("${project.buildDir}/ui")
    nodeModulesDir = file("${project.rootDir}/ui")
}

npm_run_build {
    inputs.files fileTree("ui/src")
    inputs.file 'ui/package.json'
    inputs.file 'ui/package-lock.json'
    outputs.dir 'ui/build'
}

apply from: 'project-properties.gradle'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://m2proxy.atlassian.com/repository/public" }
    if (releaseMode) {
        dependencyRepos.forEach { path ->
            maven {
                setUrl("https://maven.pkg.github.com/reportportal/${path}")
                credentials {
                    username = findProperty("githubUserName")
                    password = findProperty("githubToken")
                }
            }
        }
    } else {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:4b7ed8a')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation 'com.github.reportportal:plugin-api:574b261'
        annotationProcessor 'com.github.reportportal:plugin-api:574b261'
    }

    implementation 'com.sun.mail:javax.mail:1.6.2'

    implementation 'com.github.reportportal:commons-dao:17e4550'

    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'

    // JasperReport's export to XLS uses Apache POI
    implementation 'org.apache.poi:poi:4.1.1'
}

test {
    useJUnitPlatform()
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

build.dependsOn jacocoTestReport

artifacts {
    archives shadowJar
}

sourceSets {
    main {
        resources
                {
                    exclude '**'
                }
    }
}

jar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginId}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Healenium",
                "Plugin-Class": "com.epam.reportportal.extension.healenium.HealeniumPlugin",
                "Plugin-Service": "api"
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    configurations = [project.configurations.compile]
    zip64 true
    dependencies {
    }
}

task plugin(type: Jar) {
    getArchiveBaseName().set("plugin-${pluginId}")
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

compileJava.dependsOn npm_run_build
